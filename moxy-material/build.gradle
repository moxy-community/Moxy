apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28

        consumerProguardFiles '../proguard-rules.pro'
    }
}

configurations {
    publishConfig
}

task copyLibs(type: Copy) {
    from configurations.publishConfig
    into "libs"
}

dependencies {
    publishConfig project(":moxy-core")
    api fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly deps.android

    compileOnly project(':stub-androidx')
    compileOnly project(':stub-material')
}

preBuild.dependsOn copyLibs

def artifact = 'moxy-material'
project.archivesBaseName = artifact
group = rootProject.ext.groupId // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            groupId rootProject.ext.groupId
            artifactId artifact
            version rootProject.ext.targetVersion
            name 'Moxy Material'
            modelVersion = '4.0.0'
            description = 'Moxy Material library for Android'
            url = 'https://github.com/moxy-community/'
            inceptionYear '2019'
            licenses {
                license {
                    name 'MIT'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    name 'Moxy Community'
                    email 'moxy-community@yandex.ru'
                    organization = 'Moxy Community'
                    organizationUrl 'https://github.com/moxy-community/'
                }
            }
            scm {
                connection 'scm:git@github.com:moxy-community/Moxy.git'
                developerConnection 'scm:git@github.com:moxy-community/Moxy.git'
                url 'https://github.com/moxy-community/Moxy.git'
            }
        }
    }
}

version = getArtifactVersion('moxy-material') // Library version

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    // User and ApiKey stored in local.properties
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")

    configurations = ['archives']
    // Package info for Bintray
    pkg {
        repo = 'maven'
        name = artifact
        licenses = ['MIT']
        vcsUrl = 'https://github.com/moxy-community/Moxy'
        publish = true
        version {
            gpg {
                sign = true
                passphrase = properties.getProperty("gpgPassphrase")
            }
            mavenCentralSync {
                sync = getPublishToMaven() //[Default: true] Determines whether to sync the version to Maven Central.
                user = properties.getProperty("mavenUser") //OSS user token: mandatory
                password = properties.getProperty("mavenToken") //OSS user password: mandatory
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}

// Dependency to call only bintrayUpload task
bintrayUpload.dependsOn install