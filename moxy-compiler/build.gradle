apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: MoxyPublishing

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    implementation project(':moxy')

    implementation Deps.kotlinStdlib

    implementation Deps.javapoet

    compileOnly Deps.autocommon
    compileOnly Deps.autoservice
    compileOnly Deps.gradleIncapHelperAnnotations

    kapt Deps.gradleIncapHelperProcessor
    kapt Deps.autoservice

    testImplementation project(':moxy').sourceSets.test.output
    testImplementation Deps.junit
    testImplementation Deps.truth
    testImplementation Deps.compiletesting
    testImplementation Deps.asm

    //workaround to use test resources (https://stackoverflow.com/q/24870464)
    testRuntime files(sourceSets.test.output.resourcesDir)
}

moxyPublishing {
    artifactName 'moxy-compiler'
    pomName 'Moxy Compiler'
    pomDescription 'Moxy Compiler library for Android'
}

// Dependency to call only bintrayUpload task
bintrayUpload.dependsOn install

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
